-- =============================================
-- Author:		Andrés Felipe Londoño Campos
-- Create date: 28/01/2025
-- Description:	Función para realizar la conversión de un número a letras y/o en formato moneda colombiana.
-- =============================================
ALTER FUNCTION [dbo].[FN_NUMERO_A_LETRAS] 
(
	 @VALOR				BIGINT
	,@FORMATO_MONEDA	BIT = 0
)
RETURNS VARCHAR(MAX)
AS
BEGIN
    DECLARE   @L_VALOR			BIGINT = @VALOR
             ,@L_RETORNO_LETRAS	VARCHAR(MAX) = ''

			 ,@L_UNIDADES		BIGINT
			 ,@L_DECENAS		BIGINT
			 ,@L_CENTENTAS		BIGINT
			 ,@L_TERNA			INT = 1
			 ,@L_LETRAS			VARCHAR(MAX) = ''

	-- LIMITE MAXIMO SUPERADO
	IF(@VALOR > 999999999)
    BEGIN
		RETURN 'EL NÚMERO EXCEDE EL LÍMITE MÁXIMO PERMITIDO DE 999.999.999.'
    END

	-- SI EL VALOR INGRESADO ES 0
	IF @L_VALOR = 0
	BEGIN
		RETURN IIF(@FORMATO_MONEDA = 1, 'CERO PESOS', 'CERO')
	END

	-- CICLOS POR TERNA
	WHILE @L_VALOR > 0
	BEGIN
		SET @L_LETRAS	= ''

		SET @L_UNIDADES	= @L_VALOR % 10
		SET	@L_VALOR /= 10

		SET @L_DECENAS = @L_VALOR % 10
		SET	@L_VALOR /= 10

		SET @L_CENTENTAS = @L_VALOR % 10
		SET	@L_VALOR /= 10

		DECLARE @L_CONJUNCION VARCHAR(2) = IIF(@L_UNIDADES > 0, 'Y ', '')

		-- UNIDADES
		SELECT @L_LETRAS =
		CASE @L_UNIDADES
			WHEN 1 THEN 
				CASE @L_TERNA 
					WHEN 1 THEN 'UNO ' 
					ELSE 'UN '
				END
			WHEN 2 THEN 'DOS '
			WHEN 3 THEN 'TRES '
			WHEN 4 THEN 'CUATRO '
			WHEN 5 THEN 'CINCO '
			WHEN 6 THEN 'SEIS '
			WHEN 7 THEN 'SIETE '
			WHEN 8 THEN 'OCHO '
			WHEN 9 THEN 'NUEVE '
			ELSE @L_LETRAS
		END

		-- DECENAS
		SELECT @L_LETRAS =
		CASE @L_DECENAS 
			WHEN 1 THEN
				CASE @L_UNIDADES
					WHEN 0 THEN 'DIEZ '
					WHEN 1 THEN 'ONCE '
					WHEN 2 THEN 'DOCE '
					WHEN 3 THEN 'TRECE '
					WHEN 4 THEN 'CATORCE '
					WHEN 5 THEN 'QUINCE '
					ELSE 'DIECI' + @L_LETRAS
				END
			WHEN 2 THEN
				CASE @L_UNIDADES 
					WHEN 0 THEN 'VEINTE ' 
					ELSE 'VEINTI' + @L_LETRAS
				END
			WHEN 3 THEN 'TREINTA ' + @L_CONJUNCION + @L_LETRAS
			WHEN 4 THEN 'CUARENTA ' + @L_CONJUNCION + @L_LETRAS
			WHEN 5 THEN 'CINCUENTA ' + @L_CONJUNCION + @L_LETRAS
			WHEN 6 THEN 'SESENTA ' + @L_CONJUNCION + @L_LETRAS
			WHEN 7 THEN 'SETENTA ' + @L_CONJUNCION + @L_LETRAS
			WHEN 8 THEN 'OCHENTA ' + @L_CONJUNCION + @L_LETRAS
			WHEN 9 THEN 'NOVENTA ' + @L_CONJUNCION + @L_LETRAS
			ELSE @L_LETRAS
		END

		--CENTENAS
		SELECT @L_LETRAS =
		CASE @L_CENTENTAS
			WHEN 1 THEN IIF(@L_UNIDADES = 0 AND @L_DECENAS = 0, 'CIEN ', 'CIENTO ' + @L_LETRAS)
			WHEN 2 THEN 'DOSCIENTOS ' + @L_LETRAS
			WHEN 3 THEN 'TRESCIENTOS ' + @L_LETRAS
			WHEN 4 THEN 'CUATROCIENTOS ' + @L_LETRAS
			WHEN 5 THEN 'QUINIENTOS ' + @L_LETRAS
			WHEN 6 THEN 'SEISCIENTOS ' + @L_LETRAS
			WHEN 7 THEN 'SETECIENTOS ' + @L_LETRAS
			WHEN 8 THEN 'OCHOCIENTOS ' + @L_LETRAS
			WHEN 9 THEN 'NOVECIENTOS ' + @L_LETRAS
			ELSE @L_LETRAS
		END

		-- CASOS SIN PLURAL
		IF(@L_UNIDADES = 1 AND @L_DECENAS = 0 AND @L_CENTENTAS = 0 AND @L_TERNA = 2)
		BEGIN
			SET @L_LETRAS = ''
		END

		-- SE VERIFICA EL TAMAÑO DE LA TERNA
		SELECT @L_LETRAS =
        CASE @L_TERNA
			WHEN 1 THEN @L_LETRAS
			WHEN 2 THEN @L_LETRAS + 'MIL '
			WHEN 3 THEN IIF(@L_UNIDADES = 1 AND @L_DECENAS = 0, @L_LETRAS + 'MILLÓN ', @L_LETRAS + 'MILLONES ')
			WHEN 4 THEN @L_LETRAS + 'MIL '
			ELSE ''
        END

		-- ELIMINAR ASIGNACIÓN DE UNIDAD DE MEDIDA
		IF(@L_UNIDADES = 0 AND @L_DECENAS = 0 AND @L_CENTENTAS = 0)
		BEGIN
			SET @L_LETRAS = ''
		END

		-- RETORNO DE LA TERNA
		SET @L_RETORNO_LETRAS = @L_LETRAS + @L_RETORNO_LETRAS

		-- SE AÑADE EL CICLO
		SET @L_TERNA += 1

	END -- END WHILE
	
	-- SI SE NECESITA FORMATO PESOS
	IF @FORMATO_MONEDA = 1
	BEGIN
		SET @L_RETORNO_LETRAS = UPPER(LTRIM(RTRIM(@L_RETORNO_LETRAS))) + ' PESOS'
	END
	    
    RETURN @L_RETORNO_LETRAS
END
GO
